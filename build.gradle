buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
}

ext.versions = [
	flink		: '1.14.1',
	slf4j		: '1.7.36',
	logback	: '1.2.11',
	junit		: '4.13.2',
]

subprojects {
	apply plugin: 'java'
	apply plugin: 'signing'

	group = 'com.king.bravo'
	version = '0.6'

	repositories {
		mavenCentral()
	}

	task javadocJar(type: Jar) {
	    classifier = 'javadoc'
	    from javadoc
	}

	task sourcesJar(type: Jar) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}

	configurations.all {
		exclude module: 'commons-logging'
		exclude module: 'log4j'
		exclude module: 'slf4j-log4j12'
	}

	dependencies {

		testImplementation group: 'junit', name: 'junit', version: versions.junit
		testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
		testImplementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: versions.slf4j
		testImplementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: versions.slf4j
	}

	artifacts {
    archives javadocJar, sourcesJar
	}

	signing {
    sign configurations.archives
   }

	 tasks.withType(Sign) {
      onlyIf {project.hasProperty('release') && release}
	}

}

wrapper {
	gradleVersion = 7.5
}
